                 Prg ::= MainClass ( ClassDeclaration )* <EOF>
           MainClass ::= "class" Identifier "{" "public" "static" "void" "main" "(" "String" "[" "]" Identifier ")" ("throws" "java.io.IOException")? "{" Statement "}" "}"
    ClassDeclaration ::= "class" Identifier ( "extends" Identifier )? "{" ( VarDeclaration )* ( MethodDeclaration )* "}"
      VarDeclaration ::= Type Identifier ";"
   MethodDeclaration ::= "public" Type Identifier "(" MethodParameters ")" ("throws" "java.io.IOException")? "{" MethodBody "}"
    MethodParameters ::= ( Type Identifier ( "," Type Identifier )* )?
          MethodBody ::= ( VarDeclaration )* ( Statement )* "return" Expression ";"
                Type ::= "int" "[" "]"
                      |  "boolean"
                      |  "int"
                      |  Identifier
           Statement ::= "{" ( Statement )* "}"
                      |  "if" "(" Expression ")" Statement "else" Statement
                      |  "while" "(" Expression ")" Statement
                      |  Identifier "=" Expression ";"
                      |  Identifier "[" Expression "]" "=" Expression ";"
                      |  "System.out.println" "(" Expression ")" ";"
                      |  "System.out.write" "(" Expression ")" ";"
          Expression ::= Expression ( "&&" | "<" | "+" | "-" | "*" | "/" ) Expression
                      |  Expression "[" Expression "]"
                      |  Expression "." "length"
                      |  Expression "." Identifier "(" ( Expression ( "," Expression )* )? ")"
                      |  "System.in.read" "(" ")"
                      |  <INTEGER_LITERAL>
                      |  "true"
                      |  "false"
                      |  Identifier
                      |  "this"
                      |  "new" "int" "[" Expression "]"
                      |  "new" Identifier "(" ")"
                      |  "!" Expression
                      |  "(" Expression ")"
          Identifier ::= <IDENTIFIER>
